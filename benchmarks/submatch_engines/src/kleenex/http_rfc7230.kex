start: message_head_list

message_head_list := message_head*

eol            := /\n/
crlf           := eol
sp             := / /
htab           := /\t/
ows            := (sp | htab)*
digit          := /[0-9]/
alpha          := /[a-zA-Z]/
hexdigit       := /[0-9a-fA-F]/
unreserved     := alpha | digit | /[-._~]/
pct_encoded    := /%/ hexdigit{2}
sub_delims     := /[!$&'()*+,;=]/
pchar          := unreserved | pct_encoded | sub_delims | /[:@]/
vchar          := /[\x1f-\x7e]/
tchar          := /[-!#$%&'*+.^_`|~]/ | digit | alpha
obs_fold       := crlf (sp | htab)+
obs_text       := /[\x80-\xff]/
field_name     := tchar+
field_vchar    := vchar | obs_text
field_content  := field_vchar ((sp | htab)+ field_vchar)?
field_value    := (field_content | obs_fold)*
header_field   := "header: " field_name ~/:/ ~ows " " field_value "\n" ows
scheme         := alpha (alpha | digit | /[-+.]/)*
userinfo       := (unreserved | pct_encoded | sub_delims | /:/)*
dec_octet      := digit | /[1-9]/ digit | /1/ digit{2} | /2[0-4]/ digit | /25[0-5]/
ipv4address    := dec_octet /[.]/ dec_octet /[.]/ dec_octet /[.]/ dec_octet
h16            := hexdigit{1,4}
ls32           := h16 /:/ h16 | ipv4address
ipv6address
    :=                           (h16 /:/){6} ls32
    |                       /::/ (h16 /:/){5} ls32
    | (               h16)? /::/ (h16 /:/){4} ls32
    | ((h16 /:/){0,1} h16)? /::/ (h16 /:/){3} ls32
    | ((h16 /:/){0,2} h16)? /::/ (h16 /:/){2} ls32
    | ((h16 /:/){0,3} h16)? /::/  h16 /:/     ls32
    | ((h16 /:/){0,4} h16)? /::/              ls32
    | ((h16 /:/){0,5} h16)? /::/              h16
    | ((h16 /:/){0,6} h16)? /::/
ipvfuture      := /v/ hexdigit+ /[.]/ (unreserved | sub_delims | /:/ )+
ip_literal     := /\[/ ( ipv6address | ipvfuture ) /]/
reg_name       := (unreserved | pct_encoded | sub_delims)*
path_abempty   := (/[/]/ pchar*)*
path_absolute  := /[/]/ (pchar+ (/[/]/ pchar*)*)?
path_rootless  := pchar+ (/[/]/ pchar*)*
host           := ip_literal | ipv4address | reg_name
port           := digit*
query          := (pchar | /[/?]/)*
absolute_uri   := "scheme: " scheme "\n" ~/:/
    ( ~/[/][/]/ ("user: " userinfo "\n" ~/@/)? "host-1: " host "\n" (~/:/ "port-1: " port "\n")? "path-1: " path_abempty "\n"
    | "path-2: " (path_absolute | path_rootless)? "\n"
    ) (~/[?]/ "query-1: " query "\n")?
authority      := ("user-2: " userinfo "\n" ~/@/)? "host-2: " host "\n" (~/:/ "port-2: " port "\n")?
origin_form    := "path-3: " path_abempty "\n" (/[?]/ "query-2: " query "\n")?
http_name      := /HTTP/
http_version   := http_name /[/]/ digit /[.]/ digit
request_target := authority | absolute_uri | origin_form | /[*]/
method         := tchar+
request_line   := "method: " method "\n" ~sp request_target ~sp "" "version-2: " http_version "\n" ~crlf
status_code    := digit{3}
reason_phrase  := (htab | sp | vchar | obs_text)*
status_line    := "version-1: " http_version "\n" ~sp "status: " status_code "\n" ~sp "reason: " reason_phrase "\n" ~crlf
start_line     := (request_line | status_line)
message_head   := start_line (header_field ~crlf)* crlf

