// supported feature lists -----------------------------------------------------

supported_apis = ["simple", "generic", "record"];
supported_api_styles = ["functions", "free-form"];
supported_code_models = ["goto-label", "loop-switch", "recursive-functions"];
supported_targets = ["code", "dot", "skeleton"];
supported_features = ["nested-ifs", "bitmaps", "computed-gotos", "case-ranges",
    "tags", "captures", "captvars"];


// language-specific options ---------------------------------------------------

semicolons = 1;
implicit_bool_conversion = 1;
backtick_quoted_strings = 0;
single_quoted_strings = 0;
indentation_sensitive = 0;
wrap_blocks_in_braces = 1;


// immutable configurations (command-line only options) ------------------------

conf:target = code;
conf:code-model = goto-label;
conf:input-encoding = ascii;
conf:date = 1;
conf:version = 1;
conf:conditions = 0;
conf:storable-state = 0;
conf:flex-syntax = 0;
conf:verbose = 0;
conf:line-dirs = 1;


// mutable configurations ------------------------------------------------------

conf:api = default;
conf:api:style = functions;
conf:api:sigil = "@@";
conf:define:YYGETCOND:naked = 0;
conf:define:YYSETCOND:naked = 0;
conf:define:YYSETCOND@cond = "@@";
conf:define:YYGETSTATE:naked = 0;
conf:define:YYSETSTATE:naked = 0;
conf:define:YYSETSTATE@state = "@@";
conf:define:YYFILL@len = "@@";
conf:define:YYFILL:naked = 0;
conf:define:YYFN = ["<undefined-function-name>;<undefined-function-type>"];
conf:yyfn:sep = ";";
conf:variable:yycond = "yycond";
conf:variable:yyctable = "yyctable";
conf:variable:yyaccept = "yyaccept";
conf:variable:yytarget = "yytarget";
conf:variable:yystate = "yystate";
conf:variable:yynmatch = "yynmatch";
conf:variable:yypmatch = "yypmatch";
conf:variable:yyrecord = "yyrecord";
conf:variable:yych = "yych";
conf:variable:yych:conversion = 0;
conf:variable:yych:literals = char_or_hex;
conf:variable:yych:emit = (code_model.recursive_functions ? 0 : 1);
conf:variable:yybm = "yybm";
conf:variable:yybm:hex = 0;
conf:variable:yyfill = "";
conf:variable:yystable = ""; // deprecated
conf:header = "";
conf:eof = -1;
conf:sentinel = -1;
conf:yyfill:enable = 1;
conf:yyfill:parameter = 1;
conf:yyfill:check = 1;
conf:tags = 0;
conf:tags:prefix = "yyt";
conf:captures = 0;
conf:captvars = 0;
conf:posix-captures = 0;
conf:posix-captvars = 0;
conf:invert-captures = 0;
conf:cond:abort = (code_model.recursive_functions ? 1 : 0); // `abort()` requires include
conf:cond:prefix = "yyc_";
conf:cond:enumprefix = "yyc";
conf:cond:divider@cond = "@@";
conf:cond:goto@cond = "@@";
conf:state:abort = (code_model.recursive_functions ? 1 : 0); // `abort()` requires include
conf:state:nextlabel = 0;
conf:bit-vectors = 0;
conf:debug-output = 0;
conf:computed-gotos = 0;
conf:computed-gotos:threshold = 9;
conf:nested-ifs = 0;
conf:case-insensitive = 0;
conf:case-inverted = 0;
conf:case-ranges = 0;
conf:unsafe = 0;
conf:monadic = 0;
conf:encoding:ebcdic = 0;
conf:encoding:utf32 = 0;
conf:encoding:ucs2 = 0;
conf:encoding:utf16 = 0;
conf:encoding:utf8 = 0;
conf:encoding-policy = ignore;
conf:empty-class = match-empty;
conf:indent:string = "\t";
conf:indent:top = 0;
conf:label:prefix = "yy";
conf:label:yyfill = "yyFillLabel";
conf:label:yyloop = "";
conf:label:yyNext = "yyNext";
conf:label:start = 0;


// mutable code configuration --------------------------------------------------

conf:define:YYBACKUP = "YYBACKUP";
conf:define:YYBACKUPCTX = "YYBACKUPCTX";
conf:define:YYCONDTYPE = "YYCONDTYPE";
conf:define:YYCOPYMTAG = sigil "{lhs} = " sigil "{rhs};";
conf:define:YYCOPYSTAG = sigil "{lhs} = " sigil "{rhs};";
conf:define:YYCTXMARKER = (api.record ? yyrecord "->yyctxmarker" : "YYCTXMARKER");
conf:define:YYCTYPE = "YYCTYPE";
conf:define:YYCURSOR = (api.record ? yyrecord "->yycursor" : "YYCURSOR");
conf:define:YYDEBUG = "YYDEBUG";
conf:define:YYFILL = "YYFILL";
conf:define:YYGETACCEPT = sigil "{var}";
conf:define:YYGETCOND = "YYGETCONDITION"; // historic default
conf:define:YYGETSTATE = "YYGETSTATE";
conf:define:YYINPUT = (api.record ? yyrecord "->yyinput" : "YYINPUT");
conf:define:YYLESSTHAN = "YYLESSTHAN";
conf:define:YYLIMIT = (api.record ? yyrecord "->yylimit" : "YYLIMIT");
conf:define:YYMARKER = (api.record ? yyrecord "->yymarker" : "YYMARKER");
conf:define:YYMAXFILL = "YYMAXFILL";
conf:define:YYMAXNMATCH = "YYMAXNMATCH";
conf:define:YYMTAGN = "YYMTAGN";
conf:define:YYMTAGP = "YYMTAGP";
conf:define:YYPEEK = "YYPEEK";
conf:define:YYRESTORE = "YYRESTORE";
conf:define:YYRESTORECTX = "YYRESTORECTX";
conf:define:YYRESTORETAG = "YYRESTORETAG";
conf:define:YYSETACCEPT = sigil "{var} = " sigil "{val};";
conf:define:YYSETCOND = "YYSETCONDITION"; // historic default
conf:define:YYSETSTATE = "YYSETSTATE";
conf:define:YYSHIFT = "YYSHIFT";
conf:define:YYSHIFTSTAG = "YYSHIFTSTAG";
conf:define:YYSHIFTMTAG = "YYSHIFTMTAG";
conf:define:YYSKIP = "YYSKIP";
conf:define:YYSTAGN = "YYSTAGN";
conf:define:YYSTAGP = "YYSTAGP";
conf:tags:expression = (api.record ? yyrecord "->") sigil;
conf:tags:negative = (api.generic ? "@@" : "NULL");
conf:cond:divider = "/* *********************************** */";
conf:cond:goto = "goto " sigil ";";


// code templates --------------------------------------------------------------

code:var_local = topindent type " " name (have_init? " = " init) ";" nl;
code:var_global = code:var_local;

code:const_local = topindent type " " name " = " init ";" nl;
code:const_global = topindent "#define " name " " init nl;

code:array_local =
    topindent type " " name "[" size "] = {" nl indent
        [row{0:-2}: topindent [elem{0:-2}: elem ", "] [elem{-1}: elem ","] nl]
        [row{-1}:   topindent [elem{0:-2}: elem ", "] [elem{-1}: elem]     nl]
    dedent topindent "};" nl;
code:array_global = code:array_local;
code:array_elem = array "[" index "]";

code:type_int = "int";
code:type_uint = "unsigned int";
code:type_cond_enum = <undefined>;
code:type_yybm = "static const unsigned char";
code:type_yytarget = "static const void*";

code:assign = topindent lhs " = " rhs ";" nl;

code:cmp_eq = "==";
code:cmp_ne = "!=";
code:cmp_lt = "<";
code:cmp_gt = ">";
code:cmp_le = "<=";
code:cmp_ge = ">=";

code:if_then_else =
    [branch{0}: topindent "if (" cond ") {" nl
        indent [stmt: stmt] dedent]
    [branch{1:-1}: topindent "} else " (have_cond? "if (" cond ") ") "{" nl
        indent [stmt: stmt] dedent]
    topindent "}" nl;

code:if_then_else_oneline =
    [branch{0}:    topindent "if (" cond ") " [stmt: stmt] nl]
    [branch{1:-1}: topindent "else " (have_cond? "if (" cond ") ") [stmt: stmt] nl];

code:switch =
    topindent "switch (" expr ") {" nl
        indent [case: case] dedent
    topindent "}" nl;

code:switch_cases =
    [case: case nl]
        indent [stmt: stmt] dedent;

code:switch_cases_oneline =
    [case{0:-2}: case nl]
    [case{-1}:   case " " [stmt: stmt] nl];

code:switch_case_range = (case_ranges
    ? topindent "case " [val{0}: val] (many ? " ... " [val{-1}: val]) ":"
    : [val{0:-2}: topindent "case " val ":" nl]
      [val{-1}:   topindent "case " val ":"]);

code:switch_case_default =
    topindent "default:";

code:loop =
    topindent "for (;;) {" nl
        indent [stmt: stmt] dedent
    topindent "}" nl;

code:goto = topindent "goto " label ";" nl;

code:enum =
    "enum " name " {" nl indent
        [elem{0:-2}: topindent elem (have_init? " = " init) "," nl]
        [elem{-1}:   topindent elem (have_init? " = " init) nl]
    dedent "};" nl;

code:enum_elem = name;

code:fndecl =
    topindent type " " name "("
        [arg{0:-2}: argtype " " argname ", "]
        [arg{-1}:   argtype " " argname]
    ");" nl;

code:fndef =
    topindent type " " name "("
        [arg{0:-2}: argtype " " argname ", "]
        [arg{-1}:   argtype " " argname]
    ") {" nl
        indent [stmt: stmt] dedent
    "}" nl;

code:fncall =
    topindent (have_retval ? retval " = ") name "(" [arg{0:-2}: arg ", "] [arg{-1}: arg] ");" nl;

code:tailcall =
    topindent (have_retval ? "return ") name "(" [arg{0:-2}: arg ", "] [arg{-1}: arg] ");" nl;

code:recursive_functions =
    [fn: fndecl] nl
    [fn: fndef nl];

code:fingerprint =
    "/* Generated by re2c" (have_version? " " version) (have_date? " on " date) " */" nl;

code:line_info = "#line " line " \"" file "\"" nl;

code:abort = topindent "abort();" nl;

code:yydebug =
    topindent (api.generic
        ? YYDEBUG (api_style.functions ? "();")
    : (api.record
        ? YYDEBUG "(" yyrecord ");"
        : YYDEBUG "(" state ", " yych ");"
    )) nl;

code:yypeek =
    topindent (code_model.recursive_functions ? YYCTYPE " ") yych " = " (api.generic
        ? YYPEEK (api_style.functions ? "()")
        : (cast ? "(" YYCTYPE ")") "*" YYCURSOR
    ) ";" nl;

code:yyskip =
    topindent (api.generic
        ? YYSKIP (api_style.functions ? "();")
        : "++" YYCURSOR ";"
    ) nl;

code:yybackup =
    topindent (api.generic
        ? YYBACKUP (api_style.functions ? "();")
        : YYMARKER " = " YYCURSOR ";"
    ) nl;

code:yybackupctx =
    topindent (api.generic
        ? YYBACKUPCTX (api_style.functions ? "();")
        : YYCTXMARKER " = " YYCURSOR ";"
    ) nl;

code:yyskip_yypeek =
    topindent (code_model.recursive_functions ? YYCTYPE " ") yych " = "
        (cast ? "(" YYCTYPE ")") "*++" YYCURSOR ";" nl;

code:yypeek_yyskip =
    topindent (code_model.recursive_functions ? YYCTYPE " ") yych " = "
        (cast ? "(" YYCTYPE ")") "*" YYCURSOR "++;" nl;

code:yyskip_yybackup =
    topindent YYMARKER " = ++" YYCURSOR ";" nl;

code:yybackup_yyskip =
    topindent YYMARKER " = " YYCURSOR "++;" nl;

code:yybackup_yypeek =
    topindent (code_model.recursive_functions ? YYCTYPE " ") yych " = "
        (cast ? "(" YYCTYPE ")") "*(" YYMARKER " = " YYCURSOR ");" nl;

code:yyskip_yybackup_yypeek =
    topindent (code_model.recursive_functions ? YYCTYPE " ") yych " = "
        (cast ? "(" YYCTYPE ")") "*(" YYMARKER " = ++" YYCURSOR ");" nl;

code:yybackup_yypeek_yyskip =
    topindent (code_model.recursive_functions ? YYCTYPE " ") yych " = "
        (cast ? "(" YYCTYPE ")") "*(" YYMARKER " = " YYCURSOR "++);" nl;

code:yyrestore =
    topindent (api.generic
        ? YYRESTORE (api_style.functions ? "();")
        : YYCURSOR " = " YYMARKER ";"
    ) nl;

code:yyrestorectx =
    topindent (api.generic
        ? YYRESTORECTX (api_style.functions ? "();")
        : YYCURSOR " = " YYCTXMARKER ";"
    ) nl;

code:yyrestoretag =
    topindent (api.generic
        ? YYRESTORETAG (api_style.functions ? "(" tag ");")
        : YYCURSOR " = " tag ";"
    ) nl;

code:yyshift =
    topindent (api.generic
        ? YYSHIFT (api_style.functions ? "(-" offset ");")
        : YYCURSOR " -= " offset ";"
    ) nl;

code:yyshiftstag =
    topindent (nested ? "if (" tag " != " neg ") ") (api.generic
        ? YYSHIFTSTAG (api_style.functions ? "(" tag ", -" offset ");")
        : tag " -= " offset ";"
    ) nl;

code:yyshiftmtag =
    topindent YYSHIFTMTAG (api_style.functions ? "(" tag ", -" offset ");") nl;

code:yystagp =
    topindent (api.generic
        ? YYSTAGP (api_style.functions ? "(" tag ");")
        : tag " = " YYCURSOR ";"
    ) nl;

code:yymtagp =
    topindent YYMTAGP (api_style.functions ? "(" tag ");") nl;

code:yystagn =
    topindent (api.generic
        ? YYSTAGN (api_style.functions ? "(" tag ");")
        : tag " = " neg ";"
    ) nl;

code:yymtagn =
    topindent YYMTAGN (api_style.functions ? "(" tag ");") nl;

code:yycopystag =
    topindent (api.generic
        ? YYCOPYSTAG // function style is not supported, as there's no sensible default
        : lhs " = " rhs ";"
    ) nl;

code:yycopymtag =
    topindent (api.generic
        ? YYCOPYMTAG // function style is not supported, as there's no sensible default
        : lhs " = " rhs ";"
    ) nl;

code:yygetaccept =
    (api.generic
        ? YYGETACCEPT // function style is not supported, as there's no sensible default
        : (api.record & (storable_state | code_model.recursive_functions) ? yyrecord "->") var);

code:yysetaccept =
    topindent (api.generic
        ? YYSETACCEPT // function style is not supported, as there's no sensible default
        : (api.record & (storable_state | code_model.recursive_functions) ? yyrecord "->")
            var " = " val ";"
    ) nl;

// Historically simple API did not provide default definition for YYGETCOND.
// Changing it would break backwards compatibility, as there may be a macro definition
// not based on `yycond` that re2c is unable to see, e.g. included from a header file.
code:yygetcond =
    (api.generic | api.simple
        ? YYGETCOND (api_style.functions ? "()")
        : yyrecord "->" var);

// Historically simple API did not provide default definition for YYSETCOND.
// Changing it would break backwards compatibility, as there may be a macro definition
// not based on `yycond` that re2c is unable to see, e.g. included from a header file.
code:yysetcond =
    topindent (api.generic | api.simple
        ? YYSETCOND (api_style.functions ? "(" val ");")
        : yyrecord "->" var " = " val ";"
    ) nl;

// Historically simple API did not provide default definition for YYGETSTATE.
// Changing it would break backwards compatibility, as there may be a macro definition
// not based on `yystate` that re2c is unable to see, e.g. included from a header file.
code:yygetstate =
    (api.generic | api.simple
        ? YYGETSTATE (api_style.functions ? "()")
        : yyrecord "->" var);

// Historically simple API did not provide default definition for YYSETSTATE.
// Changing it would break backwards compatibility, as there may be a macro definition
// not based on `yystate` that re2c is unable to see, e.g. included from a header file.
code:yysetstate =
    topindent (api.generic | api.simple
        ? YYSETSTATE (api_style.functions ? "(" val ");")
        : yyrecord "->" var " = " val ";"
    ) nl;

code:yylessthan =
    (api.generic
        ? YYLESSTHAN (api_style.functions ? "(" need ")")
        : (many
            ? "(" YYLIMIT " - " YYCURSOR ") < " need
            : YYLIMIT " <= " YYCURSOR));
