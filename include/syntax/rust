api = [generic];
api_style = [freeform, functions];
jump_model = [loop_switch];
target = [code, dot];

computed_goto = unsupported;
case_ranges = unsupported;
constants = upper_case;
char_literals = hexadecimal;
semicolons = yes;
abort_in_default_case = yes;
implicit_bool_conversion = no;
backtick_quoted_strings = no;
standalone_single_quotes = yes;

code:var =
    // In Rust uninitialized variable is an error, but if the compiler is able to see
    // that all paths overwrite the initial value, it warns about unused assignments.
    (have_init? "" : topindent "#[allow(unused_assignments)]" nl)
    topindent "let mut " name " : " type " = " (have_init? init : "0") ";" nl;

code:const = topindent "const " name ": " type " = " init ";" nl;

// code:array

code:type_int = "isize";
code:type_uint = "usize";
code:type_cond_enum = (storable_state? "isize" : "usize");
// code:type_yybm
// code:type_yytarget

code:if_then_else =
    topindent "if " then_cond " {" nl
        indent [then_stmt: then_stmt] dedent
    (have_else_part? topindent "} else " (have_else_cond? "if " else_cond " ") "{" nl
        indent [else_stmt: else_stmt] dedent)
    topindent "}" nl;

code:switch =
    topindent "match " expr " {" nl
        indent [case: case] dedent
    topindent "}" nl;

code:switch_cases =
    [case{0:-2}: topindent case " |" nl]
    [case{-1}:   topindent case " => {" nl
        indent [stmt: stmt] dedent
    topindent "}" nl];

code:switch_cases_oneline =
    [case{0:-2}: topindent case " |" nl]
    [case{-1}:   topindent case " => " [stmt: stmt]];

code:switch_case_range = [val{0}: val] (multival ? " ..= " [val{-1}: val]);

code:switch_case_default = "_";

code:loop =
    topindent (have_label? label ": ") "loop {" nl
        indent [stmt: stmt] dedent
    topindent "}" nl;

// In Rust `continue` statements have labels, use it to avoid ambiguity.
code:loop_label = "'yyl";

code:enum = [elem: topindent "const " elem ": " type " = " init ";" nl];

code:enum_elem = name;

code:fingerprint =
    "/* Generated by re2c" (have_version? " " version) (have_date? " on " date) " */" nl;

// No line directives in Rust: https://github.com/rust-lang/rfcs/issues/1862.
//code:line_info

code:abort = topindent "panic!(\"internal lexer error\");" nl;

code:yypeek_expr = (unsafe? "unsafe {" peek "}" : peek) (api_style.functions ? "()");
