api = [generic];
api_style = [freeform, functions];
jump_model = [goto_label, loop_switch];
target = [code, dot];

computed_goto = unsupported;
case_ranges = unsupported;
constants = upper_case;
char_literals = (encoding.ebcdic ? hexadecimal : symbolic);
semicolons = no;
abort_requires_include = no;

code:var = topindent (have_init? name " := " init : "var " name " " type) nl;

code:const = topindent "var " name " " type " = " init nl;

code:array =
    topindent name " := [" size "]" type "{" nl indent
        [row: topindent [elem{0:-2}: elem ", "] [elem{-1}: elem ","] nl]
    dedent topindent "}" nl;

code:type_int = "int";
code:type_uint = "uint";
code:type_yybm = "byte";

code:if_then_else =
    topindent "if (" then_cond ") {" nl
        indent [then_stmt: then_stmt] dedent
    (have_else_part? topindent "} else " (have_else_cond? "if (" else_cond ") ") "{" nl
        indent [else_stmt: else_stmt] dedent)
    topindent "}" nl;

code:switch =
    topindent "switch (" expr ") {" nl
    [case: case]
    topindent "}" nl;

code:switch_cases =
    [case{0:-2}: topindent case nl
        indent topindent "fallthrough" dedent nl]
    [case{-1}: topindent case nl]
        indent [stmt: stmt] dedent;

code:switch_case_range = "case " [val{0}: val] [val{1:-1}: "," val] ":";

code:switch_case_default = "default:";

code:loop =
    (have_label? label ":" nl)
    topindent "for {" nl
        indent [stmt: stmt] dedent
    topindent "}" nl;

code:cond_enum =
    "const (" nl
        [elem{0}:    indent elem " = " (jump_model.loop_switch ? init : "iota") nl]
        [elem{1:-1}: indent elem (jump_model.loop_switch ? " = " init) nl]
    ")" nl;

code:autogen_comment =
    "// Code generated by re2c"
    (version? " " version)
    (date? " on " date)
    ", DO NOT EDIT.";

code:line_directive = "//line \"" file "\":" line nl;

code:label = name;

code:abort_expr = "panic(\"internal lexer error\")";

code:yypeek_expr = (api.generic
    ? expr (api_style.functions ? "()")
    : "*" expr );
