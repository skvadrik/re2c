supported_apis = ["default", "generic", "record"];
supported_api_styles = ["free-form", "functions"];
supported_code_models = ["goto_label", "loop_switch", "recursive_functions"];
supported_targets = ["code", "dot"];
supported_features = ["nested_ifs", "bitmaps"];

semicolons = 0;
implicit_bool_conversion = 0;
backtick_quoted_strings = 1;
standalone_single_quotes = 0;
indentation_sensitive = 0;
wrap_blocks_in_braces = 1;

conf:api = custom;
conf:api:style = free-form;
conf:api:sigil = "@@";
conf:define:YYGETCOND:naked = 0;
conf:define:YYSETCOND:naked = 0;
conf:define:YYSETCOND@cond = "@@";
conf:define:YYGETSTATE:naked = 0;
conf:define:YYSETSTATE:naked = 0;
conf:define:YYSETSTATE@state = "@@";
conf:define:YYFILL@len = "@@";
conf:define:YYFILL:naked = 0;
conf:define:YYFN = ["<undefined-function-name>;<undefined-function-type>"];
conf:yyfn:sep = ";";
conf:variable:yycond = "yycond";
conf:variable:yyctable = "yyctable";
conf:variable:yyaccept = "yyaccept";
conf:variable:yytarget = "yytarget";
conf:variable:yystate = "yystate";
conf:variable:yynmatch = "yynmatch";
conf:variable:yypmatch = "yypmatch";
conf:variable:yyrecord = "yyrecord";
conf:variable:yych = "yych";
conf:variable:yych:conversion = 0;
conf:variable:yych:literals = char_or_hex;
conf:variable:yych:emit = (code_model.recursive_functions ? 0 : 1);
conf:variable:yybm = "yybm";
conf:variable:yybm:hex = 0;
conf:variable:yyfill = "";
conf:variable:yystable = ""; // deprecated
conf:header = "";
conf:eof = -1;
conf:sentinel = -1;
conf:yyfill:enable = 1;
conf:yyfill:parameter = 1;
conf:yyfill:check = 1;
conf:tags = 0;
conf:leftmost-captures = 0;
conf:posix-captures = 0;
conf:tags:prefix = "yyt";
conf:invert-captures = 0;
conf:cond:abort = 1;
conf:cond:prefix = "yyc_";
conf:cond:enumprefix = "yyc";
conf:cond:divider@cond = "@@";
conf:cond:goto@cond = "@@";
conf:state:abort = (code_model.goto_label ? 0 : 1); // historically disabled for goto/label
conf:state:nextlabel = 0;
conf:bit-vectors = 0;
conf:debug-output = 0;
conf:computed-gotos = 0;
conf:computed-gotos:threshold = 9;
conf:nested-ifs = 0;
conf:case-insensitive = 0;
conf:case-inverted = 0;
conf:case-ranges = 0;
conf:unsafe = 0;
conf:monadic = 0;
conf:encoding:ebcdic = 0;
conf:encoding:utf32 = 0;
conf:encoding:ucs2 = 0;
conf:encoding:utf16 = 0;
conf:encoding:utf8 = 0;
conf:encoding-policy = ignore;
conf:empty-class = match-empty;
conf:indent:string = "\t";
conf:indent:top = 0;
conf:label:prefix = "yy";
conf:label:yyfill = "yyFillLabel";
conf:label:yyloop = "yyl"; // In Go `continue` statements have labels, use it to avoid ambiguity.
conf:label:yyNext = "yyNext";
conf:label:start = 0;

conf:define:YYBACKUP = "YYBACKUP";
conf:define:YYBACKUPCTX = "YYBACKUPCTX";
conf:define:YYCONDTYPE = "YYCONDTYPE";
conf:define:YYCOPYMTAG = sigil "{lhs} = " sigil "{rhs}";
conf:define:YYCOPYSTAG = sigil "{lhs} = " sigil "{rhs}";
conf:define:YYCTYPE = "YYCTYPE";
conf:define:YYCTXMARKER = (api.generic ? "YYCTXMARKER" : (api.record ? yyrecord ".") "yyctxmarker");
conf:define:YYCURSOR = (api.generic ? "YYCURSOR" : (api.record ? yyrecord ".") "yycursor");
conf:define:YYDEBUG = "YYDEBUG";
conf:define:YYFILL = "YYFILL";
conf:define:YYGETACCEPT = sigil "{var}";
conf:define:YYGETCOND = "YYGETCONDITION"; // historic default
conf:define:YYGETSTATE = "YYGETSTATE";
conf:define:YYINPUT = (api.generic ? "YYINPUT" : (api.record ? yyrecord ".") "yyinput");
conf:define:YYLESSTHAN = "YYLESSTHAN";
conf:define:YYLIMIT = (api.generic ? "YYLIMIT" : (api.record ? yyrecord ".") "yylimit");
conf:define:YYMARKER = (api.generic ? "YYMARKER" : (api.record ? yyrecord ".") "yymarker");
conf:define:YYMAXFILL = "YYMAXFILL";
conf:define:YYMAXNMATCH = "YYMAXNMATCH";
conf:define:YYMTAGN = "YYMTAGN";
conf:define:YYMTAGP = "YYMTAGP";
conf:define:YYPEEK = "YYPEEK";
conf:define:YYRESTORE = "YYRESTORE";
conf:define:YYRESTORECTX = "YYRESTORECTX";
conf:define:YYRESTORETAG = "YYRESTORETAG";
conf:define:YYSETACCEPT = sigil "{var} = " sigil "{val}";
conf:define:YYSETCOND = "YYSETCONDITION"; // historic default
conf:define:YYSETSTATE = "YYSETSTATE";
conf:define:YYSHIFT = "YYSHIFT";
conf:define:YYSHIFTSTAG = "YYSHIFTSTAG";
conf:define:YYSHIFTMTAG = "YYSHIFTMTAG";
conf:define:YYSKIP = "YYSKIP";
conf:define:YYSTAGN = "YYSTAGN";
conf:define:YYSTAGP = "YYSTAGP";
conf:tags:expression = (api.record ? yyrecord ".") sigil;
conf:tags:negative = (api.generic ? "@@" : "-1");
conf:cond:divider = "/* *********************************** */";
conf:cond:goto = "goto " sigil;

code:var_local = topindent (have_init? name " := " init : "var " name " " type) nl;
code:var_global = topindent "var " name " " type (have_init? " = " init) nl;

code:const_local = topindent "const " name " = " init nl;
code:const_global = code:const_local;

code:array_local =
    topindent name " := [" size "]" type "{" nl indent
        [row: topindent [elem{0:-2}: elem ", "] [elem{-1}: elem ","] nl]
    dedent topindent "}" nl;
code:array_global =
    topindent "var " name " = {" nl indent
        [row: topindent [elem{0:-2}: elem ", "] [elem{-1}: elem ","] nl]
    dedent topindent "}" nl;
code:array_elem = array "[" index "]";

code:type_int = "int";
code:type_uint = "uint";
// code:type_cond_enum
code:type_yybm = "byte";
// code:type_yytarget

code:assign = topindent lhs " = " rhs nl;

code:if_then_else =
    [branch{0}: topindent "if (" cond ") {" nl
        indent [stmt: stmt] dedent]
    [branch{1:-1}: topindent "} else " (have_cond? "if (" cond ") ") "{" nl
        indent [stmt: stmt] dedent]
    topindent "}" nl;

code:switch =
    topindent "switch (" expr ") {" nl
    [case: case]
    topindent "}" nl;

code:switch_cases =
    [case{0:-2}: topindent case nl
        indent topindent "fallthrough" dedent nl]
    [case{-1}: topindent case nl]
        indent [stmt: stmt] dedent;

code:switch_case_range = "case " [val{0}: val] [val{1:-1}: "," val] ":";

code:switch_case_default = "default:";

code:loop =
    (loop_label? label ":" nl)
    topindent "for {" nl
        indent [stmt: stmt] dedent
    topindent "}" nl;

code:goto = topindent "goto " label nl;

code:enum =
    "const (" nl indent
        [elem{0}:    topindent elem " = " (have_init ? init : "iota") nl]
        [elem{1:-1}: topindent elem (have_init ? " = " init) nl]
    dedent ")" nl;

code:enum_elem = name;

code:fndef =
    topindent "func " name "("
        [arg{0:-2}: argname " " argtype ", "]
        [arg{-1}:   argname " " argtype]
    ")" (have_type ? " " type) " {" nl
        indent [stmt: stmt] dedent
    "}" nl;

code:fncall =
    topindent (have_retval ? retval " = ") name "(" [arg{0:-2}: arg ", "] [arg{-1}: arg] ")" nl;

code:tailcall =
    topindent (have_retval ? "return ") name "(" [arg{0:-2}: arg ", "] [arg{-1}: arg] ")" nl;

code:recursive_functions = [fn: fndef nl];

code:fingerprint =
    "// Code generated by re2go"
    (have_version? " " version)
    (have_date? " on " date)
    ", DO NOT EDIT." nl;

code:line_info = "//line \"" file "\":" line nl;

code:abort = topindent "panic(\"internal lexer error\")" nl;

code:yydebug =
    topindent (api.generic
        ? YYDEBUG (api_style.functions ? "()")
    : (api.record
        ? YYDEBUG "(" yyrecord ")"
        : YYDEBUG "(" state ", " yych ")"
    )) nl;

code:yypeek =
    topindent yych (code_model.recursive_functions ? " := " : " = ") (api.generic
        ? YYPEEK (api_style.functions ? "()")
        : YYINPUT "[" YYCURSOR "]"
    ) nl;

code:yyskip =
    topindent (api.generic
        ? YYSKIP (api_style.functions ? "()")
        : YYCURSOR " += 1"
    ) nl;

code:yybackup =
    topindent (api.generic
        ? YYBACKUP (api_style.functions ? "()")
        : YYMARKER " = " YYCURSOR
    ) nl;

code:yybackupctx =
    topindent (api.generic
        ? YYBACKUPCTX (api_style.functions ? "()")
        : YYCTXMARKER " = " YYCURSOR
    ) nl;

code:yyrestore =
    topindent (api.generic
        ? YYRESTORE (api_style.functions ? "()")
        : YYCURSOR " = " YYMARKER
    ) nl;

code:yyrestorectx =
    topindent (api.generic
        ? YYRESTORECTX (api_style.functions ? "()")
        : YYCURSOR " = " YYCTXMARKER
    ) nl;

code:yyrestoretag =
    topindent (api.generic
        ? YYRESTORETAG (api_style.functions ? "(" tag ")")
        : YYCURSOR " = " tag
    ) nl;

code:yyshift =
    topindent (api.generic
        ? YYSHIFT (api_style.functions ? "(-" offset ")")
        : YYCURSOR " -= " offset
    ) nl;

code:yyshiftstag =
    (nested ? topindent "if (" tag " != " neg ") {" nl indent)
        topindent (api.generic
            ? YYSHIFTSTAG (api_style.functions ? "(" tag ", -" offset ")")
            : tag " -= " offset
        ) nl
    (nested ? dedent topindent "}" nl);

code:yyshiftmtag =
    topindent YYSHIFTMTAG (api_style.functions ? "(" tag ", -" offset ")") nl;

code:yystagp =
    topindent (api.generic
        ? YYSTAGP (api_style.functions ? "(" tag ")")
        : tag " = " YYCURSOR
    ) nl;

code:yymtagp =
    topindent YYMTAGP (api_style.functions ? "(" tag ")") nl;

code:yystagn =
    topindent (api.generic
        ? YYSTAGN (api_style.functions ? "(" tag ")")
        : tag " = " neg
    ) nl;

code:yymtagn =
    topindent YYMTAGN (api_style.functions ? "(" tag ")") nl;

code:yycopystag =
    topindent (api.generic
        ? YYCOPYSTAG // function style is not supported, as there's no sensible default
        : lhs " = " rhs
    ) nl;

code:yycopymtag =
    topindent (api.generic
        ? YYCOPYMTAG // function style is not supported, as there's no sensible default
        : lhs " = " rhs
    ) nl;

code:yygetaccept =
    (api.generic
        ? YYGETACCEPT // function style is not supported, as there's no sensible default
        : (api.record & (storable_state | code_model.recursive_functions) ? yyrecord ".") var);

code:yysetaccept =
    topindent (api.generic
        ? YYSETACCEPT // function style is not supported, as there's no sensible default
        : (api.record & (storable_state | code_model.recursive_functions) ? yyrecord ".")
            var " = " val
    ) nl;

code:yygetcond =
    (api.generic
        ? YYGETCOND (api_style.functions ? "()")
        : (api.record ? yyrecord ".") var);

code:yysetcond =
    topindent (api.generic
        ? YYSETCOND (api_style.functions ? "(" val ")")
        : (api.record ? yyrecord ".") var " = " val 
    ) nl;

code:yygetstate =
    (api.generic
        ? YYGETSTATE (api_style.functions ? "()")
        : (api.record ? yyrecord ".") var);

code:yysetstate =
    topindent (api.generic
        ? YYSETSTATE (api_style.functions ? "(" val ")")
        : (api.record ? yyrecord ".") var " = " val
    ) nl;

code:yylessthan =
    (api.generic
        ? YYLESSTHAN (api_style.functions ? "(" need ")")
        : (many
            ? "(" YYLIMIT " - " YYCURSOR ") < " need
            : YYLIMIT " <= " YYCURSOR));
